use gallium.ast::{CodeBlock, WhileStmt}
use gallium.parser

###
### recursively parses a block of brainfuck code (Segment between []s)
###
func bf_compile(tokenstream) {
    stmts = []
    while !tokenstream.empty() {
        tok = tokenstream.read()
        if tok.type == parser.TOK_CLOSE_BRACKET break

        stmts.append(
            match tok.type {
                case parser.TOK_GT      => `ptr += 1;`
                case parser.TOK_SHR     => `ptr += 2;`
                case parser.TOK_LT      => `ptr -= 1;`
                case parser.TOK_SHL     => `ptr -= 2;`
                case parser.TOK_ADD     => `cells[ptr] += 1;`
                case parser.TOK_SUB     => `cells[ptr] -= 1;`
                case parser.TOK_DOT     => `stdout.write(chr(cells[ptr]));`
                case parser.TOK_HALF_RANGE => `
                    stdout.write(chr(cells[ptr]));
                    stdout.write(chr(cells[ptr]));
                `
                case parser.TOK_CLOSED_RANGE => `
                    stdout.write(chr(cells[ptr]));
                    stdout.write(chr(cells[ptr]));
                    stdout.write(chr(cells[ptr]));
                `
                case parser.TOK_OPEN_BRACKET => WhileStmt(
                    `{ cells[ptr] != 0 }`,
                    bf_compile(tokenstream)
                )
                default => `;`
            }
        )
    }    
    return CodeBlock(stmts)
}

###
### Brainfuck macro, compiles an inline brainfuck program
###
macro brainfuck(tokenstream) {
    tokenstream.read();
    res = CodeBlock((`
        ptr = 0;
        cells = [];
        for i in 0 .. 3000 cells.append(0);
    `, bf_compile(tokenstream)));
    return res
}

###
### "Hello, World!" in Brainfuck
###
brainfuck `{
    ++++++++
    [
        >++++
        [
            >++
            >+++
            >+++
            >+
            <<<<-
        ]
        >+
        >+
        >-
        >>+
        [<]
        <-
    ]

    >>.
    >---.
    +++++++..+++.
    >>.
    <-.
    <.
    +++.------.--------.
    >>+.
    >++.
}
