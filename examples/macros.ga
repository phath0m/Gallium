use gallium.ast::{Call, CodeBlock, Ident, UnaryOp, WhileStmt, UNARYOP_LOGICAL_NOT}

###
### This macro transforms its arguments into a call to "puts"
###
macro say(parser) => Call(Ident("puts"), [parser.expr()])

###
### This macro implements a new statement into the language, an
### "until" loop, which loops until a condition is met
###
macro until(parser) => WhileStmt(
    UnaryOp(UNARYOP_LOGICAL_NOT, parser.expr()),
    parser.parse()
)
###
### This macro lets you compile a piece of code n times. This 
### demonstrates dynamically passing information to a macro at 
### runtime.
###
func times(n) => macro (tokenstream) {
    stmts = []
    body = tokenstream.parse()

    for i in 0 .. n {
        stmts.append(body)
    }

    return CodeBlock(stmts)
}

say`("Hello, world!")

puts("Counting to 10 with until loop")

i = 0

until `(i == 10) {
    puts(i)
    i = i + 1;
}

amount = Int(input("How many times would you like to compile the loop? "))

times(amount) `{
    puts("Hello, World!")
}
